# This is an M4 template file to create the installer script.
# The template includes files generated by the makefile. See
# the following makefile targets to change the way they are
# generated:
#  * python.files
#  * data.files
#  * conf.files

!ifndef VERSION
  !define VERSION 'anonymous-build'
!endif

!ifdef OUTFILE
  OutFile "${OUTFILE}"
!else
  OutFile acpyd-${VERSION}-setup.exe
!endif

; Location of Python
var PythonPath

InstallDir $PROGRAMFILES\Edantech\acpyd
InstallDirRegKey HKLM "Software\Edantech\acpyd" "Install_Dir"

; Request application privileges for Windows Vista
RequestExecutionLevel admin

!include "MUI.nsh"

Name "ACP245 Test Server"
Caption "ACP245 Test Server ${VERSION} Setup"
BrandingText " "

!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_BITMAP "edantech.bmp" ; optional

!define MUI_ABORTWARNING

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "..\LICENSE"
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES

!define MUI_FINISHPAGE_LINK "Visit the Edantech site for the latest news and support"
!define MUI_FINISHPAGE_LINK_LOCATION "http://www.edantech.com/"
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES


!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "PortugueseBR"
!insertmacro MUI_LANGUAGE "Spanish"

Section "acpyd" acpyd
include(python.install_files)

    SetOutPath "$PythonPath\Lib\site-packages\twisted\plugins\"
    File "..\twisted\plugins\acpyd_config.cspec"
    File "..\twisted\plugins\acpyd_plugin.pyc"

    SetOutPath "$PythonPath\Lib\site-packages\nevow\plugins\"
    File "..\nevow\plugins\acpyd_plugin.pyc"

include(data.install_files)

    SetOutPath "$INSTDIR"
    File "..\etc\acpyd.conf"
    File "..\win32\acpyd_start.bat"
    CreateDirectory "$INSTDIR\logs"

    WriteRegStr HKLM "Software\Edantech\acpyd" "Python_Install_Dir" $PythonPath

SectionEnd

Section -post # hidden required section
    SetDetailsPrint textonly
    DetailPrint "Configuring environment..."
    SetDetailsPrint listonly

    SetOutPath "$INSTDIR"
    File /oname=LICENSE.txt "..\LICENSE"

    WriteUninstaller "$INSTDIR\Uninstall.exe"
SectionEnd

;--------------------------------
;Descriptions

    ;Language strings
    LangString DESC_acpyd ${LANG_ENGLISH} "Installs the ACP245 Test Server."

    LangString DESC_acpyd ${LANG_PORTUGUESEBR} "Instala o ACP245 Test Server."

    LangString DESC_acpyd ${LANG_SPANISH} "Instala el Servidor de Pruebas ACP245."

    ;Assign language strings to sections
    !insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
        !insertmacro MUI_DESCRIPTION_TEXT ${acpyd} $(DESC_acpyd)
    !insertmacro MUI_FUNCTION_DESCRIPTION_END

Section "Uninstall"
    IfFileExists $INSTDIR\LICENSE.txt package_installed
        MessageBox MB_YESNO "It does not appear that ACP245 Test Server is installed in the directory '$INSTDIR'.$\r$\nContinue anyway (not recommended)?" IDYES package_installed
        Abort "Uninstall aborted by user"
    package_installed:

StrCmp $PythonPath "" oops ok
oops:
    Goto done
ok:
include(python.delete_files)
    Delete "$PythonPath\Lib\site-packages\nevow\plugins\acpyd_plugin.pyc"
    Delete "$PythonPath\Lib\site-packages\twisted\plugins\acpyd_plugin.pyc"
    Delete "$PythonPath\Lib\site-packages\twisted\plugins\acpyd_config.cspec"
done:

include(data.delete_files)

    Delete "$INSTDIR\acpyd.conf"
    Delete "$INSTDIR\acpyd_start.bat"
    RMDir "$INSTDIR\logs"

    DeleteRegKey HKLM "Software\Edantech\acpyd"

    Delete "$INSTDIR\Uninstall.exe"

    Delete "$INSTDIR\LICENSE.txt"

    RMDir "$INSTDIR"
    RMDir "$PROGRAMFILES\Edantech\"

SectionEnd

Function .onInit
System::Call 'kernel32::CreateMutexA(i 0, i 0, t "myMutex") i .r1 ?e'
Pop $R0

StrCmp $R0 0 +3
    MessageBox MB_OK|MB_ICONEXCLAMATION "The installer is already running."
    Abort

ReadRegStr $9 HKLM "SOFTWARE\Python\PythonCore\2.6\InstallPath" ""
StrCmp $9 "" oops ok
oops:
    ; Guess where Python 2.6 is.
    StrCpy $PythonPath "c:\Python26\"
    ; Fallback installation directory is "Program Files"
    Goto done
ok:
    StrCpy $PythonPath "$9"
done:
FunctionEnd

Function un.onInit
ReadRegStr $9 HKLM "Software\Edantech\acpyd" "Python_Install_Dir"
StrCpy $PythonPath "$9"
FunctionEnd
